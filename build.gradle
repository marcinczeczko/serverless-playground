plugins {
    id 'idea'
    id 'com.adarshr.test-logger' version '1.6.0'
    id 'io.quarkus.gradle.plugin' version '0.12.0'
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()

        maven {
            name "DynamoDB Local Release Repository - EU (Frankfurt) Region"
            url "https://s3.eu-central-1.amazonaws.com/dynamodb-local-frankfurt/release"
        }

        maven {
            url = 'http://repository.jboss.org/nexus/content/groups/public'
        }
    }
}

defaultTasks = ['build']

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

subprojects { subproject ->
    group = 'com.serverless'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.adarshr.test-logger'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testImplementation 'org.mockito:mockito-junit-jupiter:2.25.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-Xlint:deprecation'
        options.incremental = true
    }

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        useJUnitPlatform()
    }

    testlogger {
        theme 'mocha-parallel'
    }
}